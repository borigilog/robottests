*** Settings ***
Documentation     https://github.com/robotframework/HowToWriteGoodTestCases/blob/master/HowToWriteGoodTestCases.rst
Force Tags        General    Login
Metadata          Version    1.0
Metadata          Author    Marcel Mischke
Metadata          Executed At    ${RLX_LOGIN_URL}
Resource          General_PageObjects/Message_PopUp.txt
Resource          General_PageObjects/Login.txt

*** Test Cases ***
All Elements Are Available
    Open Login Page
    Inputfield Username Is Available
    Inputfield Password Is Available
    Button Submit Credentials Is Available
    Texts Are Available
    Copyright Is Available
    Links Are Available
    Links Are Clickable
    Language Selector Is Available
    Language Selector Is Clickable
    [Teardown]    Close Browser

Valid Login
    Open Login Page
    Input Username    ${RLX_USER}
    Input User Password    ${RLX_PWD}
    Submit Credentials
    Mandator Selection Page Should Be Open
    Submit Mandator Selection    ${DEFAULT_MANDATOR_NAME}
    Main Page Should Be Open
    [Teardown]    Close Browser

Invalid Username
    Open Login Page
    Input Username    Tom
    Input User Password    ${RLX_PWD}
    Submit Credentials
    Error Page Should Be Open
    [Teardown]    Close Browser

Invalid Password
    Open Login Page
    Input Username    ${RLX_USER}
    Input User Password    Taylor
    Submit Credentials
    Error Page Should Be Open
    [Teardown]    Close Browser

Invalid Username And Password
    Open Login Page
    Input Username    Tom
    Input User Password    Taylor
    Submit Credentials
    Error Page Should Be Open
    [Teardown]    Close Browser

Empty Username
    Open Login Page
    Clear Input Username
    Input User Password    ${RLX_PWD}
    Submit Credentials
    Error Page Should Be Open
    [Teardown]    Close Browser

Empty Password
    Open Login Page
    Input Username    ${RLX_USER}
    Clear Input User Password
    Submit Credentials
    Error Page Should Be Open
    [Teardown]    Close Browser

Empty Username And Password
    Open Login Page
    Clear Input Username
    Clear Input User Password
    Submit Credentials
    Error Page Should Be Open
    [Teardown]    Close Browser

*** Keywords ***
Open Login Page
    Login.Load Login Page
    Title Should Be    Rigilog: OAC

Input Username
    [Arguments]    ${username}=${EMPTY}
    Input Text    /$flow_main/login_username    ${username}

Clear Input Username
    Input Text    /$flow_main/login_username    ${SPACE}
    Clear Element Text    /$flow_main/login_username

Input User Password
    [Arguments]    ${password}=${EMPTY}
    #Input Text    /$flow_main/login_password    ${password}
    Input Password    /$flow_main/login_password    ${password}

Clear Input User Password
    Input Text    /$flow_main/login_password    ${SPACE}
    Clear Element Text    /$flow_main/login_password

Error Page Should Be Open
    Message_PopUp.Check Message PopUp Is Visible    /$flow_main    visible=true
    Check PageArea is visible    /$flow_main    screen look_rlx_screen_choose_mandator    visible=false

Submit Mandator Selection
    [Arguments]    ${mandator_name}
    Click Element    //div[contains(text(), '${mandator_name}')]
    Click Element And Wait    /$flow_main/btn_choose

Submit Credentials
    Click Element And Wait    /$flow_main/login

Mandator Selection Page Should Be Open
    Check PageArea is visible    /$flow_main    screen look_rlx_screen_choose_mandator

Main Page Should Be Open
    Check PageArea is visible    /$flow_main    screen look_rlx_screen_main_screen

Language Selector Is Available
    Language_Selector.Check LanguageSelector    /$flow_main/languages

Language Selector Is Clickable
    Language_Selector.Check Languages Are Selectable    /$flow_main/languages

Inputfield Username Is Available
    Check ElementId    /$flow_main/login_username    enabled=true
    Check LabelText    /$flow_main/login_username    Benutzername
    Textfield Value Should Be    /$flow_main/login_username    Testadmin

Inputfield Password Is Available
    Check ElementId    /$flow_main/login_password    enabled=true
    Check LabelText    /$flow_main/login_password    Passwort
    Textfield Value Should Be    /$flow_main/login_password    Tester

Button Submit Credentials Is Available
    Check ElementId    /$flow_main/login    Anmelden    enabled=true

Texts Are Available
    Check ContainsText    Login
    Check ContainsText    OAC
    Check ContainsText    Bestell- und Kommunikationsplattform für persönliche Unternehmens- und Arbeitsschutzbekleidung

Copyright Is Available
    Check ElementId    /$flow_main/copy    © Rigilog AG    enabled=true

Links Are Available
    Check ElementId    /$flow_main/link1    Rechtliches    enabled=true
    Check ElementId    /$flow_main/link2    Kontakt    enabled=true
    Check ElementId    /$flow_main/link3    http://www.katagprofession.com    enabled=true

Links Are Clickable
    Click Link    /$flow_main/link1
    Click Link    /$flow_main/link2
    Click Link    /$flow_main/link3
