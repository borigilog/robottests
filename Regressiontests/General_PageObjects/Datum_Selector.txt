*** Settings ***
Library           DateTime
Variables         Datum_Selector.yaml
Resource          ../General_Keywords/DataTypes.txt
Resource          ../General_Keywords/CommonlyUsed.txt
Resource          Language_Selector.txt
Resource          Grid.txt

*** Variables ***
# ~~~~~~~ calendar grid
${grdCalendarPopday}    //div[@class='grid look_popcal look_rlx_grid_popcal_calendar']/div[contains(@class,'gridcontainer noscroll')]/table/tbody/tr/td/button
${grdCalendarNoSelection}    //div[@class='grid look_popcal look_rlx_grid_popcal_calendar']/div[contains(@class,'gridcontainer noscroll')]/table/tbody/tr/td/div/button
# ~~~~~~~ month name
@{DATE_MONTH_NAME}    &{Dic_January}    &{Dic_February}    &{Dic_March}    &{Dic_April}    &{Dic_May}    &{Dic_June}    &{Dic_July}
...               &{Dic_August}    &{Dic_September}    &{Dic_October}    &{Dic_November}    &{Dic_December}
# ~~~~~~~ class name
${DATE_SELECTOR_POPUP_CLASS}    screen look_rlx_screen_popcal
${DATE_SELECTOR_SELECTION_CLASS}    look_popday look_sel

*** Keywords ***
Check DateSelector
    [Arguments]    ${locator}    ${labeltext}=${EMPTY}    ${inputvalue}=${EMPTY}    ${enabled}=${EMPTY}
    [Documentation]    MM 01.04.2018
    ...    check date selector element, the arguments are passed with name or in the correct order
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    ...    - ${labeltext} = value of label text, optional
    ...    - ${inputvalue} = value of input text, optional
    ...    - ${enabled} = element is enabled, optional
    ...    == Example ==
    ...    | = Keyword = | = locator = | = labeltext = | = inputvalue = | = enabled = | = Result = |
    ...    | Check DateSelector | /$flow_main/$flow_content/dateFrom | labeltext=Startdatum | inputvalue=13.04.2018 | enabled=true | check date selector => Auswertung |
    [Tags]    General, Helper, Element, DateSelector
    ${langunge_code}=    Get selected LanguageCode
    ${date_id}=    Get DateSelector DateID    ${locator}
    ${picker_id}=    Get DateSelector PickID    ${locator}
    Element Should Be Visible    ${date_id}
    Element Should Be Visible    ${picker_id}
    Run Keyword If    '${enabled}' != '${EMPTY}'    CommonlyUsed.Check Element Is Enabled    ${date_id}    ${enabled}
    Run Keyword If    '${inputvalue}' != '${EMPTY}'    Textfield Value Should Be    ${date_id}    ${inputvalue}
    Run Keyword If    '${labeltext}' != '${EMPTY}'    Check ContainsText    ${labeltext}

Check DateSelector Is Visible
    [Arguments]    ${locator}    ${visible}=${TRUE}
    [Documentation]    MM 02.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    ...    - ${visible} = element is visible, optional
    [Tags]    General, Helper, DateSelector
    Page.Check PopOver Is Visible    ${locator}    ${DATE_SELECTOR_POPUP_CLASS}    visible=${visible}

Check DateValue Is Valid
    [Arguments]    ${locator}    ${selected}=${TRUE}
    [Documentation]    MM 02.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    ...    - ${selected} = element is selected that means the date has been set, optional
    [Tags]    General, Helper, DateSelector
    ${selected}=    Is Truthy    ${selected}
    ${datevalue}=    Get Current DateValue    ${locator}
    ${langunge_code}=    Get selected LanguageCode
    ${none_value}=    Run Keyword And Return Status    Should Be Equal    ${datevalue}    &{Dic_NoValue}[${langunge_code}]
    Return From Keyword If    '${selected}' == '${FALSE}' and '${none_value}' == '${TRUE}'    ${TRUE}
    Return From Keyword If    '${selected}' == '${FALSE}' and '${datevalue}' == '${EMPTY}'    ${TRUE}
    ${passed} =    Run Keyword And Return Status    Convert DateString To DateTime    ${datevalue}
    Should Be Equal    ${selected}    ${passed}

Check Expected DateValue In DateSelector
    [Arguments]    ${locator}    ${expected_datevalue}
    [Documentation]    MM 02.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    ...    - ${expected_datevalue} = the expected date value
    [Tags]    General, Helper, DateSelector
    ${current_datevalue}=    Get Current DateValue    ${locator}
    Should Be Equal    ${current_datevalue}    ${expected_datevalue}

Check Date Is Selected
    [Arguments]    ${locator}    ${selected}=${TRUE}
    [Documentation]    MM 02.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    ...    - ${selected} = element is selected that means the date has been set, optional
    [Tags]    General, Helper, DateSelector
    ${selected}=    Is Truthy    ${selected}
    Check DateValue Is Valid    ${locator}    ${selected}
    Enforce Open DateSelector    ${locator}
    ${passed}=    Check Table Has Selection Class    ${grdCalendarPopday}    ${DATE_SELECTOR_SELECTION_CLASS}
    Should Be Equal    ${selected}    ${passed}
    Close DateSelector    ${locator}

Check Date Is Selectable
    [Arguments]    ${locator}
    [Documentation]    MM 02.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    [Tags]    General, Helper, DateSelector
    Open DateSelector    ${locator}
    Check DateSelector Is Visible    ${locator}
    Select Previous Month In DateSelector    ${locator}
    Select Next Month In DateSelector    ${locator}
    Select None Date Selection In DateSelector
    Open DateSelector    ${locator}
    Select Day In DateSelector    13

Select None Date Selection In DateSelector
    [Documentation]    MM 02.04.2018
    [Tags]    General, Helper, DateSelector
    ${langunge_code}=    Get selected LanguageCode
    ${date_id}=    Get ElementId From Table    ${grdCalendarNoSelection}    &{Dic_NoValue}[${langunge_code}]
    Click Element    ${date_id}

Select Previous Month In DateSelector
    [Arguments]    ${locator}
    [Documentation]    MM 02.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    [Tags]    General, Helper, DateSelector
    ${prev_id}=    Get DateSelector PrevID    ${locator}
    Click Element    ${prev_id}

Select Next Month In DateSelector
    [Arguments]    ${locator}
    [Documentation]    MM 02.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    [Tags]    General, Helper, DateSelector
    ${next_id}=    Get DateSelector NextID    ${locator}
    Click Element    ${next_id}

Select Month In DateSelector
    [Arguments]    ${locator}    ${datetime}
    [Documentation]    MM 02.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    ...    - ${datetime} = the datetime value
    [Tags]    General, Helper, DateSelector
    ${month_id}=    Get DateSelector MonthID    ${locator}
    ${current_month_year}=    Get Text    ${month_id}
    @{words}=    Split String    ${current_month_year}    ${SPACE}    1
    ${current_month_name}=    Get From List    ${words}    0
    ${current_month}=    Convert MonthName To Month    ${current_month_name}
    ${current_year}=    Get From List    ${words}    1
    ${current_datetime} =    Convert DateString To DateTime    1.${current_month}.${current_year}
    ${set_next_month}=    Set Variable If    '${datetime}'>'${current_datetime}'    ${TRUE}    ${False}
    ${target_month_name}=    Convert Month To MonthName    ${datetime.month}
    ${target_year}=    Convert To String    ${datetime.year}
    ${target_month_year}=    Set Variable    ${target_month_name}${SPACE}${target_year}
    : FOR    ${index}    IN RANGE    999999
    \    ${current_month_year}    Get Text    ${month_id}
    \    Exit For Loop If    '${current_month_year}'=='${target_month_year}'
    \    Run Keyword If    '${set_next_month}'=='${False}'    Select Previous Month In DateSelector    ${locator}
    \    Run Keyword If    '${set_next_month}'=='${TRUE}'    Select Next Month In DateSelector    ${locator}

Select Day In DateSelector
    [Arguments]    ${day}
    [Documentation]    MM 02.04.2018
    [Tags]    General, Helper, DateSelector
    ${day}=    Convert To String    ${day}
    ${date_id}=    Get ElementId From Table    ${grdCalendarPopday}    ${day}
    Click Element    ${date_id}

Open DateSelector
    [Arguments]    ${locator}
    [Documentation]    MM 02.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    [Tags]    General, Helper, DateSelector
    ${picker_id}=    Get DateSelector PickID    ${locator}
    Click Element    ${picker_id}

Enforce Open DateSelector
    [Arguments]    ${locator}
    [Documentation]    MM 02.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    [Tags]    General, Helper, DateSelector
    ${picker_visible} =    Run Keyword And Return Status    Check DateSelector Is Visible    ${locator}
    Run Keyword If    '${picker_visible}' == '${FALSE}'    Log    Der Datumsselektor ist nicht sichtbar und muss daher ge√∂ffnet werden!    WARN
    Run Keyword If    '${picker_visible}' == '${FALSE}'    Open DateSelector    ${locator}

Close DateSelector
    [Arguments]    ${locator}
    [Documentation]    MM 02.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    [Tags]    General, Helper, DateSelector
    Page.Click MouseOutSide PopOver

Set Date In DateSelector
    [Arguments]    ${locator}    ${datevalue}
    [Documentation]    MM 02.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    ...    - ${datevalue} = the date value
    [Tags]    General, Helper, DateSelector
    ${datetime}=    Convert DateString To DateTime    ${datevalue}
    Open DateSelector    ${locator}
    Select Month In DateSelector    ${locator}    ${datetime}
    Select Day In DateSelector    ${datetime.day}

Edit DateValue In DateSelector
    [Arguments]    ${locator}    ${datevalue}
    [Documentation]    MM 02.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    ...    - ${datevalue} = the date value
    [Tags]    General, Helper, DateSelector
    ${date_id}=    Get DateSelector DateID    ${locator}
    Input Text    ${date_id}    ${datevalue}

Get Selected Day In DateSelector
    [Documentation]    MM 02.04.2018
    [Tags]    General, Helper, DateSelector
    @{webelements}=    Get WebElements    ${grdCalendarPopday}
    : FOR    ${element}    IN    @{webelements}
    \    ${class}=    Set Variable    ${element.get_attribute('class')}
    \    Continue For Loop If    '${class}'!='look_popday look_sel'
    \    Return From Keyword    ${element.text}
    [Return]    ${-1}

Get DateTime Now
    ${datetime}=    Get Current Date    result_format=datetime
    [Return]    ${datetime}

Get DateString Now
    ${date}=    Get Current Date    result_format=%d.%m.%Y
    [Return]    ${date}

Get Current DateValue
    [Arguments]    ${locator}
    [Documentation]    MM 02.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    ${date_id}=    Get DateSelector DateID    ${locator}
    ${datevalue}=    Get Value    ${date_id}
    [Return]    ${datevalue}

Convert Month To MonthName
    [Arguments]    ${month}
    [Documentation]    MM 02.04.2018
    ...    get selected month
    [Tags]    General, Helper, DateSelector
    ${month}=    Evaluate    ${month}-${1}
    ${month_dic}=    Get From List    ${DATE_MONTH_NAME}    ${month}
    ${length}=    Get Length    ${month_dic}
    Return From Keyword If    '${length}'=='${0}'    Fail    Der Monat (${month}) konnte nicht gefunden werden
    ${langunge_code}=    Get selected LanguageCode
    ${monthName}=    Get From Dictionary    ${monthDic}    ${langunge_code}
    [Return]    ${monthName}

Convert MonthName To Month
    [Arguments]    ${month_name}
    [Documentation]    MM 02.04.2018
    ...    get selected month
    [Tags]    General, Helper, DateSelector
    ${langunge_code}=    Get selected LanguageCode
    ${stripped_month_name}=    Get Stripped String    ${month_name}
    ${index}=    Set Variable    ${0}
    : FOR    ${monthDic}    IN    @{DATE_MONTH_NAME}
    \    ${index}=    Evaluate    ${index}+${1}
    \    ${isMonthName}=    Get From Dictionary    ${monthDic}    ${langunge_code}
    \    Return From Keyword If    '${isMonthName}'=='${stripped_month_name}'    ${index}
    [Return]    Fail    Der Monat (${month_name}) konnte nicht gefunden werden

Get DateSelector PickID
    [Arguments]    ${locator}
    [Documentation]    MM 13.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    ${id}=    Set Variable    ${locator}/pick
    [Return]    ${id}

Get DateSelector NextID
    [Arguments]    ${locator}
    [Documentation]    MM 13.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    Run Keyword And Return    Page.Get ElementID In PopOver    ${locator}    ${DATE_SELECTOR_POPUP_CLASS}    next

Get DateSelector PrevID
    [Arguments]    ${locator}
    [Documentation]    MM 13.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    Run Keyword And Return    Page.Get ElementID In PopOver    ${locator}    ${DATE_SELECTOR_POPUP_CLASS}    prev

Get DateSelector MonthID
    [Arguments]    ${locator}
    [Documentation]    MM 13.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    Run Keyword And Return    Page.Get ElementID In PopOver    ${locator}    ${DATE_SELECTOR_POPUP_CLASS}    month

Get DateSelector DateID
    [Arguments]    ${locator}
    [Documentation]    MM 13.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all date selector elements , e.g.: /$flow_main/$flow_content/dateFrom/pick \ = /$flow_main/$flow_content/dateFrom
    ${id}=    Set Variable    ${locator}/date
    [Return]    ${id}
