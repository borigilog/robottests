*** Settings ***

*** Variables ***
&{Dic_CostCenterPopUpTitle}    D=Kostenstellen    E=Cost centers    F=Centres de coûts
# ~~~~~~~ class name
${COSTCENTER_POPUP_CLASS}    screen look_rlx_screen_choose_costcenter

*** Keywords ***
Check CostCenter Selection Is Available
    [Arguments]    ${locator}
    [Documentation]    MM 11.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    [Tags]    General, Helper, CostCenter_PopUp
    ${picker_id}=    Get CostCenter PickID    ${locator}
    ${remove_id}=    Get CostCenter RemoveID    ${locator}
    ${name_id}=    Get CostCenter NameID    ${locator}
    Element Should Be Visible    ${picker_id}
    Element Should Be Visible    ${remove_id}
    Element Should Be Visible    ${name_id}

Check CostCenter PopUp
    [Arguments]    ${locator}
    [Documentation]    MM 11.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    [Tags]    General, Helper, CostCenter_PopUp
    ${title_id}=    Get CostCenter TitleID    ${locator}
    ${close_id}=    Get CostCenter CloseID    ${locator}
    Element Should Be Visible    ${close_id}
    ${langunge_code}=    Get selected LanguageCode
    Run Keyword And Continue On Failure    Element Text Should Be    ${title_id}    &{Dic_CostCenterPopUpTitle}[${langunge_code}]

Open CostCenter PopUp
    [Arguments]    ${locator}
    [Documentation]    MM 11.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    [Tags]    General, Helper, CostCenter_PopUp
    ${picker_id}=    Get CostCenter PickID    ${locator}
    Click Element    ${picker_id}

Close CostCenter PopUp
    [Arguments]    ${locator}
    [Documentation]    MM 11.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    [Tags]    General, Helper, CostCenter_PopUp
    ${close_id}=    Get CostCenter CloseID    ${locator}
    Click Element    ${close_id}

Remove Selected CostCenter In CostCenter PopUp
    [Arguments]    ${locator}
    [Documentation]    MM 11.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    [Tags]    General, Helper, CostCenter_PopUp
    ${remove_id}=    Get CostCenter RemoveID    ${locator}
    Click Element    ${remove_id}

Check CostCenter PopUp Is Visible
    [Arguments]    ${locator}    ${visible}=${TRUE}
    [Documentation]    MM 11.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    ...    - ${visible} = element is visible , e.g.: true or false, optional
    [Tags]    General, Helper, CostCenter_PopUp
    Page.Check PopOver Is Visible    ${locator}    ${COSTCENTER_POPUP_CLASS}    visible=${visible}

Get Current CostCenterValue In CostCenter PopUp
    [Arguments]    ${locator}
    [Documentation]    MM 13.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    [Tags]    General, Helper, CostCenter_PopUp
    ${name_id}=    Get CostCenter NameID    ${locator}
    ${value}=    Get Value    ${name_id}
    [Return]    ${value}

Check Expected Value In CostCenter PopUp
    [Arguments]    ${locator}    ${expected_value}
    [Documentation]    MM 13.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    ...    - ${expected_value} = expected value of element
    [Tags]    General, Helper, CostCenter_PopUp
    ${current_value}=    Get Current CostCenterValue In CostCenter PopUp    ${locator}
    Should Be Equal    ${current_value}    ${expected_value}

Set CostCenter In CostCenter PopUp
    [Arguments]    ${locator}    @{costsenter_node_names}
    [Documentation]    MM 11.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    ...    - @{costsenter_node_names} = dictionary of all costcenternames in order of their nodes, necessary to open the nodes
    ...    == Example ==
    ...    @{costcenter_names}= Create List KST_BVG KST_BVG KST-Verwaltung KST-Verwaltung Werkstätten VAG Werkstätten Bus Bus
    ...    | = Keyword = | = locator = | = costcenter_names = | = Result = |
    ...    | Set CostCenter In CostCenter PopUp | /$flow_main/$flow_content/cost_center | @{costcenter_names} | set costcentername => Bestellinformationen |
    [Tags]    General, Helper, CostCenter_PopUp
    Open CostCenter PopUp    ${locator}
    ${length} =    Get Length    ${costsenter_node_names}
    ${id}=    Get CostCenter FirstTreeItemID    ${locator}
    ${count}=    Set Variable    ${0}
    : FOR    ${value}    IN    @{costsenter_node_names}
    \    ${count}=    Evaluate    ${count}+${1}
    \    ${id}=    Get Id From Node For CostCenter PopUp    ${id}    ${value}
    \    ${btn} =    Set Variable If    '${count}' == '${length}'    $icon    $btn
    \    Click Element    ${id}/${btn}
    \    ${id}=    Set Variable    ${id}_

Get Id From Node For CostCenter PopUp
    [Arguments]    ${locator}    ${value}
    [Documentation]    MM 11.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    ...    - ${value} = value of element (costcenter name)
    [Tags]    General, Helper, CostCenter_PopUp
    Wait Until Costcenter Node Has Children    ${locator}
    ${node_location}=    Set Variable    //div[contains(@class,'treecontainer')]/div[contains(@id,'${locator}')][contains(@class,'treenode')]/div[contains(@class,'treenodecontent')]/div[contains(@class,'text')]
    @{webelements} =    Get WebElements    ${node_location}
    : FOR    ${element}    IN    @{webelements}
    \    Continue For Loop If    '${value}'!='${element.text}'
    \    ${id}=    Set Variable    ${element.get_attribute('id')}
    \    ${id}=    Replace String    ${id}    /$txt    ${EMPTY}    count=1
    \    Return From Keyword    ${id}
    [Return]    ${EMPTY}

Wait Until Costcenter Node Has Children
    [Arguments]    ${locator}
    [Documentation]    MM 11.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    [Tags]    General, Helper, CostCenter_PopUp
    ${node_location}=    Set Variable    //div[contains(@class,'treecontainer')]/div[contains(@id,'${locator}')]
    : FOR    ${index}    IN RANGE    60
    \    ${length} =    Get Element Count    ${node_location}
    \    Exit For Loop If    '${length}'>'${0}'
    \    Sleep    1    Wait Until TreeNode Has Children

Get CostCenter PickID
    [Arguments]    ${locator}
    [Documentation]    MM 13.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    ${id}=    Set Variable    ${locator}/pick
    [Return]    ${id}

Get CostCenter FirstTreeItemID
    [Arguments]    ${locator}
    [Documentation]    MM 13.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    Run Keyword And Return    Page.Get ElementID In PopOver    ${locator}    ${COSTCENTER_POPUP_CLASS}    cctree/1

Get CostCenter CloseID
    [Arguments]    ${locator}
    [Documentation]    MM 13.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    Run Keyword And Return    Page.Get ElementID In PopOver    ${locator}    ${COSTCENTER_POPUP_CLASS}    $close

Get CostCenter TitleID
    [Arguments]    ${locator}
    [Documentation]    MM 13.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    Run Keyword And Return    Page.Get ElementID In PopOver    ${locator}    ${COSTCENTER_POPUP_CLASS}    $title

Get CostCenter NameID
    [Arguments]    ${locator}
    [Documentation]    MM 13.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    ${id}=    Set Variable    ${locator}/name
    [Return]    ${id}

Get CostCenter RemoveID
    [Arguments]    ${locator}
    [Documentation]    MM 13.04.2018
    ...
    ...    == Parameters ==
    ...    - ${locator} = the general path of all cost center elements , e.g.: /$flow_main/$flow_content/cost_center/pick \ = /$flow_main/$flow_content/cost_center
    ${id}=    Set Variable    ${locator}/remove
    [Return]    ${id}
