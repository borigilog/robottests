*** Settings ***
Variables         Menu_Common.yaml
Resource          Tooltip.txt
Resource          Language_Selector.txt
Resource          Page.txt
Resource          Grid.txt
Resource          ../General_Keywords/CommonlyUsed.txt
Resource          ../General_Variables/DefaultValues.txt

*** Variables ***
${txtMandatorname}    /$flow_main/menu_bottom/mandatorname
${btnSwitchmandator}    /$flow_main/menu_bottom/switchmandator
${switchmandator_class}    screen look_rlx_screen_switch_mandator
${btnHelp}        /$flow_main/menu_bottom/help
${help_class}     screen look_rlx_screen_contact_info
${btnExit}        /$flow_main/menu_bottom/exit
${exit_class}     screen look_rlx_screen_login

*** Keywords ***
Check ToolTip SwitchMandator
    [Tags]    ToolTip    TextLanguagedependent    Action
    ${langunge_code}=    Get selected LanguageCode
    Check TooltipId    ${btnSwitchmandator}    &{Dic_Mandantenauswahl}[${langunge_code}]

Check ToolTip Help
    [Tags]    ToolTip    TextLanguagedependent    Action
    ${langunge_code}=    Get selected LanguageCode
    Check TooltipId    ${btnHelp}    &{Dic_Kontakt}[${langunge_code}]

Check ToolTip Exit
    [Tags]    ToolTip    TextLanguagedependent    Action
    ${langunge_code}=    Get selected LanguageCode
    Check TooltipId    ${btnExit}    &{Dic_Modul_verlassen}[${langunge_code}]

Check Toolbox Is Avaliable
    #Mandant
    Check ElementId    ${txtMandatorname}    ${DEFAULT_MANDATOR_NAME}    enabled=true
    Check ElementId    ${btnSwitchmandator}    enabled=true
    Check ElementId    ${btnHelp}    enabled=true
    Check ElementId    ${btnExit}    enabled=true
    #Sprachauswahl
    Check LanguageSelector Title
    Check LanguageSelector

Check Userbox Is Avaliable
    ${langunge_code}=    Get selected LanguageCode
    Check ContainsText    &{Dic_Benutzer}[${langunge_code}]
    Check ElementId    ${user_name}    ${DEFAULT_USER_NAME}    enabled=true

Select Switchmandator
    Select Menu Content    ${btnSwitchmandator}    ${switchmandator_class}

Select Help
    Select Menu Content    ${btnHelp}    ${help_class}

Select Exit
    Select Menu Content    ${btnExit}    ${exit_class}    content_id=/$flow_main

Select Menu Content
    [Arguments]    ${element_id}    ${class}    ${content_id}=/$flow_main/$flow_content
    [Documentation]    MM 22.03.2018, select certain Menu and waits until the menu content is loaded/displaed
    ...
    ...    == Parameters ==
    ...    - ${element_id} = the id of the submenu
    ...    - ${class} = the class of the container-div that is displayed when the menu content is loaded
    ...
    ...    == Example ==
    ...    | = Keyword = | = element_id = | = class = | = Result = |
    ...    | Select Menu Content | /$flow_main/main_menu/order_initial | screen look_rlx_screen_search_person | opens menu "Eintritt: MA ausstatten" within the OAC-modul |
    Check ElementId    ${element_id}
    Click Element And Wait    ${element_id}
    Check PageArea is visible    ${content_id}    ${class}

Select Submenu Content
    [Arguments]    ${element_id}    ${class}    ${content_id}=/$flow_main/$flow_content
    [Documentation]    MM 12.04.2018, select certain Submenu and waits until the menu content is loaded/displaed
    ...
    ...    == Parameters ==
    ...    - ${element_id} = the id of the submenu
    ...    - ${class} = the class of the container-div that is displayed when the menu content is loaded
    ...
    ...    == Example ==
    ...    | = Keyword = | = element_id = | = class = | = Result = |
    ...    | Select Submenu Content | /$flow_main/main_menu/returns_return | screen look_rlx_screen_screen_return | opens menu "Reklamationen/Retouren" -> "Retoure erfassen" within the OAC-modul |
    ${parent_node_location}=    Set Variable    //div[contains(@id,'${element_id}')]/parent::div/parent::div
    ${parent_element} =    Get WebElement    ${parent_node_location}
    ${parent_id} =    Get Element Attribute    ${parent_element}    id
    Mouse Over    ${parent_id}
    Wait Until Element is Visible    ${element_id}
    Select Menu Content    ${element_id}    ${class}    ${content_id}

Select SubMenue
    [Arguments]    ${Hauptmenüpunkt}    ${Untermenüpunkt}=None
    Run Keyword If    '${Hauptmenüpunkt}' != ''    Wait Until Page Contains Element    //div[contains(text(),'${Hauptmenüpunkt}')]
    Click Element    //div[contains(text(),'${Hauptmenüpunkt}')]/..
    Comment    Click Element    //div[contains(@id,'admin_other')]
    Comment    ${x}=    Get Horizontal Position    //div[contains(@id,'admin_other')]
    Comment    ${y}=    Get Vertical Position    //div[contains(@id,'admin_other')]
    Comment    Click Element At Coordinates    //div[contains(@id,'admin_other')]    ${x}    ${y}
    Run Keyword If    '${Untermenüpunkt}' != 'None'    Wait Until Element is Visible    //div[contains(text(),'${Untermenüpunkt}')]
    Run Keyword If    '${Untermenüpunkt}' != 'None'    Click Element    //div[contains(text(),'${Untermenüpunkt}')]
