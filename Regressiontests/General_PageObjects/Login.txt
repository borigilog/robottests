*** Settings ***
Library           Selenium2Library    run_on_failure=Nothing
Resource          Module_Selector.txt
Resource          ../General_Variables/Website_Config.txt
Resource          ../General_Variables/DefaultValues.txt
Resource          Page.txt

*** Keywords ***
Load Login Page
    Open Browser    ${RLX_LOGIN_URL}    ${BROWSER}
    Maximize Browser Window
    Wait until Page is unfreezed
    Wait Until Page Contains Element    //*[@id="/$flow_main"][contains(@class, 'screen look_rlx_screen_login')]

Select Default Mandator
    [Arguments]    ${mandator_name}=${DEFAULT_MANDATOR_NAME}
    Click Element    //div[contains(text(), '${mandator_name}')]
    Click Element And Wait    /$flow_main/btn_choose

Login For Module
    [Arguments]    ${module_name}    ${mandator_name}=${DEFAULT_MANDATOR_NAME}
    Login
    Select Default Mandator    ${mandator_name}
    Check PageArea is visible    /$flow_main    screen look_rlx_screen_main_screen
    Module_Selector.Select Module By Name    ${module_name}

Login
    Load Login Page
    Input Text    /$flow_main/login_username    ${RLX_USER}
    Input Password    /$flow_main/login_password    ${RLX_PWD}
    Click Element And Wait    /$flow_main/login
    Check PageArea is visible    /$flow_main    screen look_rlx_screen_choose_mandator
