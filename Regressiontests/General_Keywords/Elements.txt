*** Settings ***
Library           Selenium2Library    run_on_failure=Nothing
Library           Collections
Resource          DataTypes.txt

*** KeyWords ***
Get Elements Text Data
    [Arguments]    ${locator}    ${data_type}=string
    [Documentation]    Returns the texts of all WebElements matching provided locator converted into provided data_type
    @{webelements} =    Get WebElements    ${locator}
    ${values}=    Create List
    : FOR    ${el}    IN    @{webelements}
    \    ${content}=    Get Webelement Data    ${el}    ${data_type}
    \    Append To List    ${values}    ${content}
    [Return]    ${values}

Get Webelement Data
    [Arguments]    ${webelement}    ${data_type}=string
    Run Keyword And Return If    '${data_type}' == 'checkbox'    Get Selection of CheckBox    ${webelement.get_attribute('id')}
    Run Keyword And Return    Convert Text To Data Type    ${webelement.text}    ${data_type}

Get Elements Texts
    [Arguments]    ${locator}
    [Documentation]    Returns a list of the texts of all WebElements matching provided locator
    @{webelements} =    Get WebElements    ${locator}
    ${values}=    Create List
    : FOR    ${el}    IN    @{webelements}
    \    Append To List    ${values}    ${el.text}
    [Return]    ${values}
