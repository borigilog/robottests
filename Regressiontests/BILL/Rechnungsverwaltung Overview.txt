*** Settings ***
Documentation     Menüpunktes BILL -> Rechnungsverwaltung: Rechnungsübersicht
Suite Setup       Select Rechnungsverwaltung    true
Suite Teardown    Close Browser
Force Tags        Bill    Rechnungsverwaltung
Default Tags
Resource          ../General_Keywords/Collections.txt
Resource          PageObjects/Menu.txt
Resource          PageObjects/Rechnungsverwaltung_Overview.txt
Resource          ../General_Variables/Website_Config.txt
Resource          ../General_PageObjects/Language_Selector.txt
Resource          ../General_PageObjects/Grid.txt
Library           ../../Libs/ActionChainsExtension.py
Variables         PageObjects/Rechnungsverwaltung_Overview.yaml

*** Test Cases ***
Select no Bill
    [Documentation]    BILL -> Rechnungsverwaltung -> Übersicht: Status der Buttons, wenn kein Element selektiert ist
    [Tags]    BillSelection
    Filters Should Be Enabled
    Element Should Be Enabled    ${btnCreateBill}
    Element Should Be Enabled    ${btnAbort}
    Element Should Be Disabled    ${btnRecalculate}
    Element Should Be Disabled    ${btnEditBill}
    Element Should Be Disabled    ${btnDeleteBill}
    Element Should Be Disabled    ${btnReopenBill}
    Element Should Be Disabled    ${btnCloseBill}
    Element Should Be Disabled    ${btnProcessBill}
    Element Should Be Disabled    ${btnExportBills}

Select Bill in Status "In Bearbeitung"
    [Documentation]    BILL -> Rechnungsverwaltung -> Übersicht: Status der Buttons, wenn Rechnung selektiert ist im Status "In Bearbeitung"
    [Tags]    BillSelection
    Click Element    ${billOpen}
    Filters Should Be Enabled
    Element Should Be Enabled    ${btnCreateBill}
    Element Should Be Enabled    ${btnAbort}
    Element Should Be Enabled    ${btnRecalculate}
    Element Should Be Enabled    ${btnEditBill}
    Element Should Be Enabled    ${btnDeleteBill}
    Element Should Be Disabled    ${btnReopenBill}
    Element Should Be Enabled    ${btnCloseBill}
    Element Should Be Enabled    ${btnProcessBill}
    Element Should Be Enabled    ${btnExportBills}

Select Bill in Status "Verarbeitet"
    [Documentation]    BILL -> Rechnungsverwaltung -> Übersicht: Status der Buttons, wenn Rechnung selektiert ist im Status "Verarbeitet"
    [Tags]    BillSelection
    Click Element    ${billProcessed}
    Filters Should Be Enabled
    Element Should Be Enabled    ${btnCreateBill}
    Element Should Be Enabled    ${btnAbort}
    Element Should Be Disabled    ${btnRecalculate}
    Element Should Be Enabled    ${btnEditBill}
    Element Should Be Enabled    ${btnDeleteBill}
    Element Should Be Disabled    ${btnReopenBill}
    Element Should Be Disabled    ${btnCloseBill}
    Element Should Be Disabled    ${btnProcessBill}
    Element Should Be Enabled    ${btnExportBills}

Select Bill in Status "Verarbeitet & veröffentlicht"
    [Documentation]    BILL -> Rechnungsverwaltung -> Übersicht: Status der Buttons, wenn Rechnung selektiert ist im Status "Verarbeitet"
    [Tags]    BillSelection
    Click Element    ${billProcessedClosed}
    Filters Should Be Enabled
    Element Should Be Enabled    ${btnCreateBill}
    Element Should Be Enabled    ${btnAbort}
    Element Should Be Disabled    ${btnRecalculate}
    Element Should Be Enabled    ${btnEditBill}
    Element Should Be Enabled    ${btnDeleteBill}
    Element Should Be Disabled    ${btnReopenBill}
    Element Should Be Disabled    ${btnCloseBill}
    Element Should Be Disabled    ${btnProcessBill}
    Element Should Be Enabled    ${btnExportBills}

Select Bill in Status "Veröffentlicht"
    [Documentation]    BILL -> Rechnungsverwaltung -> Übersicht: Status der Buttons, wenn Rechnung selektiert ist im Status "Veröffentlicht"
    [Tags]    BillSelection
    Click Element    ${billClosed}
    Filters Should Be Enabled
    Element Should Be Enabled    ${btnCreateBill}
    Element Should Be Enabled    ${btnAbort}
    Element Should Be Disabled    ${btnRecalculate}
    Element Should Be Enabled    ${btnEditBill}
    Element Should Be Enabled    ${btnDeleteBill}
    Element Should Be Enabled    ${btnReopenBill}
    Element Should Be Disabled    ${btnCloseBill}
    Element Should Be Enabled    ${btnProcessBill}
    Element Should Be Enabled    ${btnExportBills}

Select Multiple Bills Via CONTROL
    [Documentation]    BILL -> Rechnungsverwaltung -> Übersicht: Status der Buttons, wenn mehrere Elemente selektiert sind mit CONTROL (STRG)
    [Tags]    BillSelection
    Select Elements With CONTROL    ${billProcessed}    ${billProcessedClosed}
    Bill Should Be Selected    ${billProcessed}
    Bill Should Be Selected    ${billProcessedClosed}
    Check Buttons Enabled For Multiple Bills

Select Multiple Bills Via SHIFT
    [Documentation]    BILL -> Rechnungsverwaltung -> Übersicht: Status der Buttons, wenn mehrere Elemente selektiert sind mit SHIFT
    [Tags]    BillSelection
    Select Elements With SHIFT    ${billProcessed}    ${billProcessedClosed}
    Bill Should Be Selected    ${billProcessed}
    Bill Should Be Selected    ${billProcessedClosed}
    Check Buttons Enabled For Multiple Bills

Check Languagedependent Text Content
    [Documentation]    Bill -> Rechnungsverwaltung -> Übersicht: Prüft, ob pro Sprache die richtigen Texte der (statischen) Modul-Dictionaries angezeigt werden
    [Tags]    Languagedependent
    [Setup]    Init Status Dictionaries
    [Template]    Template Check Language Content
    : FOR    ${key}    IN    @{AVAILABLE_LANGUAGES.keys()}
    \    ${key}

Sort By Rechnungs-Nr
    [Documentation]    BILL -> Rechnungsverwaltung -> Übersicht: Sortierung nach Spalte "Rechnungs-Nr."
    [Tags]    Sorting
    Check Sorting Table By Columnname    ${grdBillsPrefix}    invoiceNumber

Sort By Rechnungsbezeichnung
    [Documentation]    BILL -> Rechnungsverwaltung -> Übersicht: Sortierung nach Spalte "Rechnungsbezeichnung"
    [Tags]    Sorting
    Check Sorting Table By Columnname    ${grdBillsPrefix}    description    caseinsensitive=${TRUE}

Sort By Status
    [Documentation]    BILL -> Rechnungsverwaltung -> Übersicht: Sortierung nach Spalte "Status"
    [Tags]    Sorting
    Check Sorting Table By Columnname    ${grdBillsPrefix}    status    caseinsensitive=${TRUE}

Sort By Erstellt am
    [Documentation]    BILL -> Rechnungsverwaltung -> Übersicht: Sortierung nach Spalte "Erstellt am"
    [Tags]    Sorting
    Check Sorting Table By Columnname    ${grdBillsPrefix}    creationDate    data_type=datetime

Sort By Positionen
    [Documentation]    BILL -> Rechnungsverwaltung -> Übersicht: Sortierung nach Spalte "Positionen"
    [Tags]    Sorting
    Check Sorting Table By Columnname    ${grdBillsPrefix}    items    data_type=int

Sort By Proforma
    [Documentation]    BILL -> Rechnungsverwaltung -> Übersicht: Sortierung nach Spalte "Proforma"
    [Tags]    Sorting
    Check Sorting Table By Columnname    ${grdBillsPrefix}    proForma    data_type=checkbox

Filter Bills
    [Tags]    Filter
    [Setup]    Create Bill Status Test Variables
    [Template]    Template Test Filter
    showClosedBills=${TRUE}    showProcessedBills=${TRUE}    showProformaBills=${TRUE}
    showClosedBills=${TRUE}    showProcessedBills=${TRUE}    showProformaBills=${FALSE}
    showClosedBills=${TRUE}    showProcessedBills=${FALSE}    showProformaBills=${TRUE}
    showClosedBills=${TRUE}    showProcessedBills=${FALSE}    showProformaBills=${FALSE}
    showClosedBills=${FALSE}    showProcessedBills=${TRUE}    showProformaBills=${TRUE}
    showClosedBills=${FALSE}    showProcessedBills=${TRUE}    showProformaBills=${FALSE}
    showClosedBills=${FALSE}    showProcessedBills=${FALSE}    showProformaBills=${TRUE}
    showClosedBills=${FALSE}    showProcessedBills=${FALSE}    showProformaBills=${FALSE}

Close Rechnungsverwaltung
    Click Element    ${btnAbort}
    Check Homescreen is selected

Neue Rechnung eröffnen
    Click Element    ${btnCreateBill}
    Check PageArea is visible    ${Screens_Container}    ${Screen_CreateBill}

*** KeyWords ***
Template Test Filter
    [Arguments]    ${showClosedBills}=${TRUE}    ${showProcessedBills}=${TRUE}    ${showProformaBills}=${TRUE}
    Apply Filter    showClosedBills = ${showClosedBills}    showProcessedBills=${showProcessedBills}    showProformaBills=${showProformaBills}
    Check If Bills Are Filtered    showClosedBills = ${showClosedBills}    showProcessedBills=${showProcessedBills}    showProformaBills=${showProformaBills}

Template Check Language Content
    [Arguments]    ${key}
    Set LanguageId    ${key}
    ${language_code} =    Get Selected LanguageCode
    Run Keyword And Continue On Failure    Element Text Should Be    ${lblHeadline}    &{DicYAML_Rechnungsverwaltung}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${colProforma}    &{DicYAML_Proforma}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${colBillNo}    &{DicYAML_Rechnungs-Nr.}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${colBillName}    &{DicYAML_Rechnungsbezeichnung}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${colStatus}    &{DicYAML_Status}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${colDateOfCreation}    &{DicYAML_Erstellt am}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${colPositionCount}    &{DicYAML_Positionen}[${language_code}]
    Run Keyword And Continue On Failure    Check LabelText    ${chkShowClosedBills}    &{DicYAML_Auch bereits veröffentlichte Rechnungen mit anzeigen}[${language_code}]
    Run Keyword And Continue On Failure    Check LabelText    ${chkShowProcessedBills}    &{DicYAML_Auch bereits verarbeitete Rechnungen mit anzeigen}[${language_code}]
    Run Keyword And Continue On Failure    Check LabelText    ${chkShowProformaBills}    &{DicYAML_Auch Proformarechnungen mit anzeigen}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${btnCreateBill}    &{DicYAML_Neue Rechnung eröffnen}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${btnEditBill}    &{DicYAML_Rechnung bearbeiten}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${btnDeleteBill}    &{DicYAML_Rechnung löschen}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${btnReopenBill}    &{DicYAML_Rechnung zurückziehen}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${btnCloseBill}    &{DicYAML_Rechnung veröffentlichen}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${btnProcessBill}    &{DicYAML_Rechnung ist verarbeitet}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${btnRecalculate}    &{DicYAML_Preise erneut berechnen}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${btnAbort}    &{DicYAML_Vorgang abbrechen}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${btnExportBills}    &{DicYAML_Rechnungsexport}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${billOpen}/$status    &{Bills_State_InProgress}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${billProcessed}/$status    &{Bills_State_IsProcessed}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${billClosed}/$status    &{Bills_State_Published}[${language_code}]
    Run Keyword And Continue On Failure    Element Text Should Be    ${billProcessedClosed}/$status    &{Bills_State_IsProcessed_and_Published}[${language_code}]

Bill Should Be Selected
    [Arguments]    ${id}
    ${cls} =    Get Element Attribute    ${id}    class
    Should Be Equal    ${cls}    sel

Check Buttons Enabled For Multiple Bills
    Filters Should Be Enabled
    Element Should Be Enabled    ${btnCreateBill}
    Element Should Be Enabled    ${btnAbort}
    Element Should Be Disabled    ${btnRecalculate}
    Element Should Be Disabled    ${btnEditBill}
    Element Should Be Disabled    ${btnDeleteBill}
    Element Should Be Disabled    ${btnReopenBill}
    Element Should Be Disabled    ${btnCloseBill}
    Element Should Be Disabled    ${btnProcessBill}
    Element Should Be Disabled    ${btnExportBills}

Filters Should Be Enabled
    Element Should Be Enabled    ${chkShowClosedBills}
    Element Should Be Enabled    ${chkShowProcessedBills}
    Element Should Be Enabled    ${chkShowProformaBills}

Check If Bills Are Filtered
    [Arguments]    ${showClosedBills}=${TRUE}    ${showProcessedBills}=${TRUE}    ${showProformaBills}=${TRUE}
    ${showClosedBills} =    Is Truthy    ${showClosedBills}
    ${showProcessedBills} =    Is Truthy    ${showProcessedBills}
    ${showProformaBills} =    Is Truthy    ${showProformaBills}
    ${showProcessedClosedBills} =    Evaluate    ${showProcessedBills} or ${showClosedBills}
    ${content} =    Get Content Of Table Column    ${grdBillsPrefix}    status
    ${openBillsExist} =    List Contains Value    ${content}    u'${status_InProgress}'
    ${closedBillsExist} =    List Contains Value    ${content}    u'${status_Published}'
    ${processedBillsExist} =    List Contains Value    ${content}    u'${status_Processed}'
    ${processedClosedBillsExist} =    List Contains Value    ${content}    u'${status_ProcessedAndPublished}'
    Should Be Equal    ${openBillsExist}    ${TRUE}
    Should Be Equal    ${closedBillsExist}    ${showClosedBills}
    Should Be Equal    ${processedBillsExist}    ${showProcessedBills}
    Should Be Equal    ${processedClosedBillsExist}    ${showProcessedClosedBills}

Create Bill Status Test Variables
    Init Status Dictionaries
    ${language_code} =    Get Selected LanguageCode
    Set Test Variable    ${status_InProgress}    &{Bills_State_InProgress}[${language_code}]
    Set Test Variable    ${status_Published}    &{Bills_State_Published}[${language_code}]
    Set Test Variable    ${status_Processed}    &{Bills_State_IsProcessed}[${language_code}]
    Set Test Variable    ${status_ProcessedAndPublished}    &{Bills_State_IsProcessed_and_Published}[${language_code}]
