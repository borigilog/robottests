*** Settings ***
Suite Setup       Setup Rechnungsausgabe
Suite Teardown    Close Browser
Test Teardown     Teardown Rechnungsausgabe
Force Tags        Bill    Rechnungsausgabe
Default Tags
Metadata          Version    1.0
Metadata          Author    Marcel Mischke
Resource          PageObjects/Menu.txt
Resource          ../General_PageObjects/Message_PopUp.txt
Resource          ../General_Variables/Pathes.txt
Resource          ../General_Variables/Database_Config.txt
Resource          ../General_Variables/DefaultValues.txt
Library           ${LIBS_PATH}/DatabaseClass.py    database=${DATABASE_NAME}    user=${DATABASE_USER}    password=${DATABASE_PWD}    host=${DATABASE_IP}    port=${DATABASE_PORT}
Library           ${LIBS_PATH}/SQLQueryReader.py

*** Variables ***
# ~~~~~~~~~ checkboxes
${chkShowOnlyOpenBills}    /$flow_main/$flow_content/ShowOnlyOpenBills
${chkShowProcessedBills}    /$flow_main/$flow_content/ShowProcessedBills
${chkShowProformaBills}    /$flow_main/$flow_content/ShowProformaBills
${chkPrintLogo}    /$flow_main/$flow_content/Print_Logo
${chkAddCopy}     /$flow_main/$flow_content/cbx_AddCopy
# ~~~~~~~~~ textfield
${txtEmail}       /$flow_main/$flow_content/email
# ~~~~~~~ buttons
${btnCreateBill}    /$flow_main/$flow_content/createBill
${btnAbort}       /$flow_main/$flow_content/abort
${btnThirdParty}    /$flow_main/$flow_content/thirdParty
${btnProForma}    /$flow_main/$flow_content/proForma
# ~~~~~~~ invoices list
${listInvoice}    /$flow_main/$flow_content/issue_list
# ~~~~~~~ invoices col
${colInvoiceNumber}    ${listInvoice}/$col_invoiceNumber
${colDescription}    ${listInvoice}/$col_description
${colStatus}      ${listInvoice}/$col_status
${colCreationDate}    ${listInvoice}/$col_creationDate
${colItems}       ${listInvoice}/$col_items

*** Test Cases ***
Check GUI Elements Rechnungsausgabe
    [Documentation]    MM 22.03.2018, Check GUI Elements for Rechnugsausgabe
    [Tags]    GUI_Elements    TextLanguagedependent
    ${langunge_code}=    Get selected LanguageCode
    #title
    Run Keyword And Continue On Failure    Check ContainsText    &{Dic_Title}[${langunge_code}]
    #grid
    Run Keyword And Continue On Failure    Check ElementId    ${colInvoiceNumber}    &{Dic_colInvoiceNumber}[${langunge_code}]    enabled=true
    Run Keyword And Continue On Failure    Check ElementId    ${colDescription}    &{Dic_colDescription}[${langunge_code}]    enabled=true
    Run Keyword And Continue On Failure    Check ElementId    ${colStatus}    &{Dic_colStatus}[${langunge_code}]    enabled=true
    Run Keyword And Continue On Failure    Check ElementId    ${colCreationDate}    &{Dic_colCreationDate}[${langunge_code}]    enabled=true
    Run Keyword And Continue On Failure    Check ElementId    ${colItems}    &{Dic_colItems}[${langunge_code}]    enabled=true
    #checkbox
    Run Keyword And Continue On Failure    Check CheckboxId    ${chkShowOnlyOpenBills}    &{Dic_chkShowOnlyOpenBills}[${langunge_code}]    enabled=true    selected=false
    Run Keyword And Continue On Failure    Check CheckboxId    ${chkShowProcessedBills}    &{Dic_chkShowProcessedBills}[${langunge_code}]    enabled=true    selected=false
    Run Keyword And Continue On Failure    Check CheckboxId    ${chkShowProformaBills}    &{Dic_chkShowProformaBills}[${langunge_code}]    enabled=true    selected=false
    Run Keyword And Continue On Failure    Check CheckboxId    ${chkPrintLogo}    &{Dic_chkPrintLogo}[${langunge_code}]    enabled=true    selected=false
    Run Keyword And Continue On Failure    Check CheckboxId    ${chkAddCopy}    &{Dic_chkAddCopy}[${langunge_code}]    enabled=true    selected=true
    #textfield
    Run Keyword And Continue On Failure    Check TextfieldId    ${txtEmail}    ${DEFAULT_EMAIL_ADRESS}    enabled=true    labeltext=&{Dic_txtEmail}[${langunge_code}]
    #buttons
    Run Keyword And Continue On Failure    Check ElementId    ${btnThirdParty}    &{Dic_btnThirdParty}[${langunge_code}]    enabled=false
    Run Keyword And Continue On Failure    Check ElementId    ${btnProForma}    &{Dic_btnProForma}[${langunge_code}]    enabled=false
    Run Keyword And Continue On Failure    Check ElementId    ${btnAbort}    &{Dic_btnAbort}[${langunge_code}]    enabled=true

Check Text Languagedependent
    [Tags]    TextLanguagedependent    ChangeLanguage
    : FOR    ${key}    IN    @{AVAILABLE_LANGUAGES.keys()}
    \    Set LanguageId    ${key}
    \    Check LanguageId is selected    ${key}
    \    ${langunge_code}=    Get selected LanguageCode
    \    Run Keyword And Continue On Failure    Check ContainsText    &{Dic_Title}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Element Text Should Be    ${colInvoiceNumber}    &{Dic_colInvoiceNumber}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Element Text Should Be    ${colDescription}    &{Dic_colDescription}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Element Text Should Be    ${colStatus}    &{Dic_colStatus}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Element Text Should Be    ${colCreationDate}    &{Dic_colCreationDate}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Element Text Should Be    ${colItems}    &{Dic_colItems}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Check LabelText    ${chkShowOnlyOpenBills}    &{Dic_chkShowOnlyOpenBills}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Check LabelText    ${chkShowProcessedBills}    &{Dic_chkShowProcessedBills}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Check LabelText    ${chkShowProformaBills}    &{Dic_chkShowProformaBills}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Check LabelText    ${chkPrintLogo}    &{Dic_chkPrintLogo}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Check LabelText    ${chkAddCopy}    &{Dic_chkAddCopy}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Check ContainsText    &{Dic_txtEmail}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Element Text Should Be    ${btnThirdParty}    &{Dic_btnThirdParty}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Element Text Should Be    ${btnProForma}    &{Dic_btnProForma}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Element Text Should Be    ${btnAbort}    &{Dic_btnAbort}[${langunge_code}]
    Set LanguageId    ${DEFAULT_LANGUAGE_ID}

Check Valid Email
    [Tags]    Action
    Input Text    ${txtEmail}    support@rigilog.com
    Check ElementId with class is visible    ${txtEmail}    invalid    visible=false
    Input Text    ${txtEmail}    support@rigilog
    Check ElementId with class is visible    ${txtEmail}    invalid    visible=true
    Input Text    ${txtEmail}    ''
    Check ElementId with class is visible    ${txtEmail}    invalid    visible=true

Check Sort By Rechnungs-Nr
    [Documentation]    BILL -> Rechnungsausgabe -> Übersicht: Sortierung nach Spalte "Rechnungs-Nr."
    [Tags]    Sorting
    Check Sorting Table By Columnname    ${listInvoice}    $invoiceNumber

Check Sort By Rechnungsbezeichnung
    [Documentation]    BILL -> Rechnungsausgabe -> Übersicht: Sortierung nach Spalte "Rechnungsbezeichnung."
    [Tags]    Sorting
    Check Sorting Table By Columnname    ${listInvoice}    $description

Check Sort By Status
    [Documentation]    BILL -> Rechnungsausgabe -> Übersicht: Sortierung nach Spalte "Status."
    [Tags]    Sorting
    Check Sorting Table By Columnname    ${listInvoice}    $status

Check Sort By Erstellt am
    [Documentation]    BILL -> Rechnungsausgabe -> Übersicht: Sortierung nach Spalte "Erstellt am."
    [Tags]    Sorting
    Check Sorting Table By Columnname    ${listInvoice}    $creationDate    data_type=datetime

Check Sort By Positionen
    [Documentation]    BILL -> Rechnungsausgabe -> Übersicht: Sortierung nach Spalte "Positionen."
    [Tags]    Sorting
    Check Sorting Table By Columnname    ${listInvoice}    $items    data_type=int

Check No Invoice Selected
    [Documentation]    BILL -> Rechnungsausgabe: Status der Buttons, wenn kein Element selektiert ist
    [Tags]    Action
    ${invoiceIsSelected}    Check List has selection    ${listInvoice}
    Should Be Equal    ${invoiceIsSelected}    ${FALSE}
    Check CheckboxId    ${chkShowOnlyOpenBills}    enabled=true    selected=false
    Check CheckboxId    ${chkShowProcessedBills}    enabled=true    selected=false
    Check CheckboxId    ${chkShowProformaBills}    enabled=true    selected=false
    Check CheckboxId    ${chkPrintLogo}    enabled=true    selected=false
    Check CheckboxId    ${chkAddCopy}    enabled=true    selected=false
    Check ElementId    ${btnThirdParty}    enabled=false
    Check ElementId    ${btnProForma}    enabled=false
    Check ElementId    ${btnAbort}    enabled=true

Select Open Invoice DBConnection
    [Documentation]    BILL -> Rechnungsausgabe: Status der Buttons, wenn Rechnung selektiert ist im Status "In Bearbeitung"
    [Tags]    Action    DBConnection
    ${langunge_code}=    Get selected LanguageCode
    ${invoiceIsSelected}    Check List has selection    ${listInvoice}
    Should Be Equal    ${invoiceIsSelected}    ${FALSE}
    ${queryResultInvNo}=    Get InvoiceNo From Database    Retrieve_OpenBills
    ${InvoiceID}    Get InvoiceID    ${listInvoice}    $invoiceNumber    ${queryResultInvNo}
    ${selectInvoice}=    Check Invoice is selectable    ${InvoiceID}
    Run Keyword If    ${selectInvoice}    Select InvoiceID    ${InvoiceID}
    Run Keyword If    ${selectInvoice}    Element Text Should Be    ${InvoiceID}/$status    &{Dic_IsOpen}[${langunge_code}]
    Check CheckboxId    ${chkShowOnlyOpenBills}    enabled=true    selected=false
    Check CheckboxId    ${chkShowProcessedBills}    enabled=true    selected=false
    Check CheckboxId    ${chkShowProformaBills}    enabled=true    selected=false
    Check CheckboxId    ${chkPrintLogo}    enabled=true    selected=false
    Check CheckboxId    ${chkAddCopy}    enabled=true    selected=false
    Check ElementId    ${btnThirdParty}    enabled=${selectInvoice}
    Check ElementId    ${btnProForma}    enabled=${selectInvoice}
    Check ElementId    ${btnAbort}    enabled=true

Select Complete Invoice DBConnection
    [Documentation]    BILL -> Rechnungsausgabe: Status der Buttons, wenn Rechnung selektiert ist im Status "Veröffentlicht"
    [Tags]    Action    DBConnection
    ${langunge_code}=    Get selected LanguageCode
    ${invoiceIsSelected}    Check List has selection    ${listInvoice}
    Should Be Equal    ${invoiceIsSelected}    ${FALSE}
    Click Element And Wait    ${chkShowOnlyOpenBills}
    ${queryResultInvNo}=    Get InvoiceNo From Database    Retrieve_CompleteBills
    ${InvoiceID}    Get InvoiceID    ${listInvoice}    $invoiceNumber    ${queryResultInvNo}
    ${selectInvoice}=    Check Invoice is selectable    ${InvoiceID}
    Run Keyword If    ${selectInvoice}    Select InvoiceID    ${InvoiceID}
    Run Keyword If    ${selectInvoice}    Element Text Should Be    ${InvoiceID}/$status    &{Dic_IsComplete}[${langunge_code}]
    Check CheckboxId    ${chkShowOnlyOpenBills}    enabled=true    selected=true
    Check CheckboxId    ${chkShowProcessedBills}    enabled=true    selected=false
    Check CheckboxId    ${chkShowProformaBills}    enabled=true    selected=false
    Check CheckboxId    ${chkPrintLogo}    enabled=true    selected=false
    Check CheckboxId    ${chkAddCopy}    enabled=true    selected=false
    Check ElementId    ${btnThirdParty}    enabled=${selectInvoice}
    Check ElementId    ${btnProForma}    enabled=${selectInvoice}
    Check ElementId    ${btnAbort}    enabled=true

Select Processed Invoice DBConnection
    [Documentation]    BILL -> Rechnungsausgabe: Status der Buttons, wenn Rechnung selektiert ist im Status "Verarbeitet"
    [Tags]    Action    DBConnection
    ${langunge_code}=    Get selected LanguageCode
    ${invoiceIsSelected}    Check List has selection    ${listInvoice}
    Should Be Equal    ${invoiceIsSelected}    ${FALSE}
    Click Element And Wait    ${chkShowProcessedBills}
    ${queryResultInvNo}=    Get InvoiceNo From Database    Retrieve_ProcessedBills
    ${InvoiceID}    Get InvoiceID    ${listInvoice}    $invoiceNumber    ${queryResultInvNo}
    ${selectInvoice}=    Check Invoice is selectable    ${InvoiceID}
    Run Keyword If    ${selectInvoice}    Select InvoiceID    ${InvoiceID}
    Run Keyword If    ${selectInvoice}    Element Text Should Be    ${InvoiceID}/$status    &{Dic_IsProcessed}[${langunge_code}]
    Check CheckboxId    ${chkShowOnlyOpenBills}    enabled=true    selected=false
    Check CheckboxId    ${chkShowProcessedBills}    enabled=true    selected=true
    Check CheckboxId    ${chkShowProformaBills}    enabled=true    selected=false
    Check CheckboxId    ${chkPrintLogo}    enabled=true    selected=false
    Check CheckboxId    ${chkAddCopy}    enabled=true    selected=false
    Check ElementId    ${btnThirdParty}    enabled=false
    Check ElementId    ${btnProForma}    enabled=false
    Check ElementId    ${btnAbort}    enabled=true

Select Processed Closed Invoice DBConnection
    [Documentation]    BILL -> Rechnungsausgabe: Status der Buttons, wenn Rechnung selektiert ist im Status "Verarbeitet & veröffentlicht"
    [Tags]    Action    DBConnection
    ${langunge_code}=    Get selected LanguageCode
    ${invoiceIsSelected}    Check List has selection    ${listInvoice}
    Should Be Equal    ${invoiceIsSelected}    ${FALSE}
    Click Element And Wait    ${chkShowProcessedBills}
    ${queryResultInvNo}=    Get InvoiceNo From Database    Retrieve_CompleteAndProcessedBills
    ${InvoiceID}    Get InvoiceID    ${listInvoice}    $invoiceNumber    ${queryResultInvNo}
    ${selectInvoice}=    Check Invoice is selectable    ${InvoiceID}
    Run Keyword If    ${selectInvoice}    Select InvoiceID    ${InvoiceID}
    Run Keyword If    ${selectInvoice}    Element Text Should Be    ${InvoiceID}/$status    &{Dic_IsCompleteAndProcessed}[${langunge_code}]
    Check CheckboxId    ${chkShowOnlyOpenBills}    enabled=true    selected=false
    Check CheckboxId    ${chkShowProcessedBills}    enabled=true    selected=true
    Check CheckboxId    ${chkShowProformaBills}    enabled=true    selected=false
    Check CheckboxId    ${chkPrintLogo}    enabled=true    selected=false
    Check CheckboxId    ${chkAddCopy}    enabled=true    selected=false
    Check ElementId    ${btnThirdParty}    enabled=false
    Check ElementId    ${btnProForma}    enabled=false
    Check ElementId    ${btnAbort}    enabled=true

Select Proforma Invoice DBConnection
    [Documentation]    BILL -> Rechnungsausgabe: Status der Buttons, wenn auch Profomarechnung anzeigen selektiert ist
    [Tags]    Action    DBConnection
    ${langunge_code}=    Get selected LanguageCode
    ${invoiceIsSelected}    Check List has selection    ${listInvoice}
    Should Be Equal    ${invoiceIsSelected}    ${FALSE}
    Click Element And Wait    ${chkShowProformaBills}
    ${queryResultInvNo}=    Get InvoiceNo From Database    Retrieve_OpenProformaBills
    ${InvoiceID}    Get InvoiceID    ${listInvoice}    $invoiceNumber    ${queryResultInvNo}
    ${selectInvoice}=    Check Invoice is selectable    ${InvoiceID}
    Run Keyword If    ${selectInvoice}    Select InvoiceID    ${InvoiceID}
    Run Keyword If    ${selectInvoice}    Element Text Should Be    ${InvoiceID}/$status    &{Dic_IsOpen}[${langunge_code}]
    Check CheckboxId    ${chkShowOnlyOpenBills}    enabled=true    selected=false
    Check CheckboxId    ${chkShowProcessedBills}    enabled=true    selected=false
    Check CheckboxId    ${chkShowProformaBills}    enabled=true    selected=true
    Check CheckboxId    ${chkPrintLogo}    enabled=true    selected=false
    Check CheckboxId    ${chkAddCopy}    enabled=true    selected=false
    Check ElementId    ${btnThirdParty}    enabled=${selectInvoice}
    Check ElementId    ${btnProForma}    enabled=${selectInvoice}
    Check ElementId    ${btnAbort}    enabled=true

Click Logo mit aufdrucken
    [Tags]    Action
    ${passed} =    Run Keyword And Return Status    Click Element And Wait    ${chkPrintLogo}
    Should Be Equal    ${passed}    ${TRUE}

Click Kopie anfügen
    [Tags]    Action
    ${passed}    Run Keyword And Return Status    Click Element And Wait    ${chkAddCopy}
    Should Be Equal    ${passed}    ${TRUE}

Click Button Rechnung mit Positionen
    [Tags]    Action
    ${InvoiceID}    Get InvoiceID with Positions    ${listInvoice}
    Select InvoiceID    ${InvoiceID}
    Click Element And Wait    ${btnThirdParty}
    Message_PopUp.Check Message PopUp Is Visible    visible=false

Click Button Rechnung ohne Positionen
    [Tags]    Action
    ${InvoiceID}    Get InvoiceID    ${listInvoice}    $items    0
    Select InvoiceID    ${InvoiceID}
    Click Element And Wait    ${btnThirdParty}
    Message_PopUp.Check Message PopUp Is Visible    visible=true
    Message_PopUp.Check Error Message PopUp    &{Dic_ErrorMsg}
    Message_PopUp.Click Close Button
    Message_PopUp.Check Message PopUp Is Visible    visible=false
    Click Element And Wait    ${btnThirdParty}
    Message_PopUp.Check Message PopUp Is Visible    visible=true
    Message_PopUp.Click OK Button
    Message_PopUp.Check Message PopUp Is Visible    visible=false

Click Button Proformarechnung mit Positionen
    [Tags]    Action
    ${InvoiceID}    Get InvoiceID with Positions    ${listInvoice}
    Select InvoiceID    ${InvoiceID}
    Click Element And Wait    ${btnThirdParty}
    Message_PopUp.Check Message PopUp Is Visible    visible=false

Click Button Proformarechnung ohne Positionen
    [Tags]    Action
    ${InvoiceID}    Get InvoiceID    ${listInvoice}    $items    0
    Select InvoiceID    ${InvoiceID}
    Click Element And Wait    ${btnProForma}
    Message_PopUp.Check Message PopUp Is Visible    visible=true
    Message_PopUp.Check Error Message PopUp    &{Dic_ErrorMsg}
    Message_PopUp.Click Close Button
    Message_PopUp.Check Message PopUp Is Visible    visible=false
    Click Element And Wait    ${btnProForma}
    Message_PopUp.Check Message PopUp Is Visible    visible=true
    Message_PopUp.Click OK Button
    Message_PopUp.Check Message PopUp Is Visible    visible=false

Click Button Zurück zum Hauptmenü
    [Tags]    Action
    Click Element And Wait    ${btnAbort}
    Check PageArea is visible    ${menu_content}    ${invoice_issue_class}    visible=false
    Check Homescreen is selected

*** Keywords ***
Get InvoiceID
    [Arguments]    ${Id}    ${Col}    ${Value}=${EMPTY}
    ${Value}=    Get Stripped String    ${Value}
    Run Keyword If    '${Value}' == '${EMPTY}'    Log    Es konnte keine entsprechende Rechnung gefunden werden!    WARN
    ${ElementId}    Get ElementID from List    ${Id}    ${Col}    ${Value}
    Run Keyword If    '${Value}' != '${EMPTY}' and '${ElementId}' == '${EMPTY}'    Fail    Die Rechnung (${Value}) konnte nicht gefunden werden!
    [Return]    ${ElementId}

Check Invoice is selectable
    [Arguments]    ${Id}
    ${selectInvoice}=    Set Variable if    '${Id}'=='${EMPTY}'    ${FALSE}    ${TRUE}
    Run Keyword If    '${selectInvoice}' == '${TRUE}'    Log    Eine entsprechende Rechnung wurde gefunden und kann ausgewählt werden    INFO
    Run Keyword If    '${selectInvoice}' == '${FALSE}'    Log    Eine entsprechende Rechnung wurde nicht gefunden und kann auch nicht ausgewählt werden    WARN
    [Return]    ${selectInvoice}

Select InvoiceID
    [Arguments]    ${Id}
    Click Element And Wait    ${Id}
    Check List ElementId is selected    ${Id}    sel

Get InvoiceID with Positions
    [Arguments]    ${element_id}    ${endRange}=999999
    : FOR    ${index}    IN RANGE    ${endRange}
    \    ${passed} =    Run Keyword And Return Status    Element Should Be Visible    ${element_id}/${index}
    \    Return From Keyword If    '${passed}'!='${TRUE}'    ${EMPTY}
    \    ${colValue} =    Get Text    ${element_id}/${index}/$items
    \    Return From Keyword If    '${colValue}'>'0'    ${element_id}/${index}
    [Return]    ${EMPTY}

Get InvoiceNo From Database
    [Arguments]    ${query_name}
    ${sql} =    SQLQueryReader.Get Query By Name    ${SQL_QUERIES_PATH}/BILL/SQLQueries.resx    ${query_name}
    ${queryResult}=    DatabaseClass.DB Execute Scalar With Parameter    ${sql}    ${DEFAULT_MANDATOR_NAME}
    [Return]    ${queryResult}

Setup Rechnungsausgabe
    Select Rechnungsausgabe    true
    Create Dictionaries For Rechnungsausgabe

Teardown Rechnungsausgabe
    ${passed} =    Run Keyword And Return Status    Select Rechnungsausgabe
    Return From Keyword If    ${passed}
    Log    Die Seite war nicht clickable daher muss der Browser geschlossen und neugestartet werden    WARN
    Close Browser
    Select Rechnungsausgabe    true

Create Dictionaries For Rechnungsausgabe
    Create SettingsDics    Billing_Print    HeadLine    Dic_Title
    Create SettingsDics    Bills    Billing_No    Dic_colInvoiceNumber
    Create SettingsDics    Bills    Billing_Name    Dic_colDescription
    Create SettingsDics    Bills    Billing_State    Dic_colStatus
    Create SettingsDics    Bills    Billing_Created_the    Dic_colCreationDate
    Create SettingsDics    Bills    Positions    Dic_colItems
    Create SettingsDics    Billing_Print    ShowOnlyOpenBills    Dic_chkShowOnlyOpenBills
    Create SettingsDics    Bills    ShowProcessedBills    Dic_chkShowProcessedBills
    Create SettingsDics    Billing_Print    ShowProformaBills    Dic_chkShowProformaBills
    Create SettingsDics    Billing_Print    Print_Logo    Dic_chkPrintLogo
    Create SettingsDics    Billing_Print    cbx_AddCopy    Dic_chkAddCopy
    Create SettingsDics    Billing_Print    Receipient    Dic_txtEmail
    Create SettingsDics    Billing_Print    To_ThirtParty    Dic_btnThirdParty
    Create SettingsDics    Billing_Print    BTN_Bill_Proforma    Dic_btnProForma
    Create SettingsDics    General    Back_ToMain    Dic_btnAbort
    Create SettingsDics    Bills    State_InProgress    Dic_IsOpen
    Create SettingsDics    Bills    State_IsProcessed    Dic_IsProcessed
    Create SettingsDics    Bills    State_Published    Dic_IsComplete
    Create SettingsDics    Bills    State_IsProcessed_and_Published    Dic_IsCompleteAndProcessed
    Create SettingsDics    Billing_Print    ERROR_Bill_Send    Dic_ErrorMsg
