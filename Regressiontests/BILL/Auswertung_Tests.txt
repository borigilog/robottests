*** Settings ***
Suite Setup       Setup Auswertung
Suite Teardown    Close Browser
Test Teardown     Teardown Auswertung
Force Tags        Bill    Auswertung
Default Tags
Metadata          Version    1.0
Metadata          Author    Marcel Mischke
Resource          ../../UserKeywords/Common_Resources.html
Resource          PageObjects/Menu.txt

*** Variables ***
# ~~~~~~~~~ date selector
${selStartDate}    /$flow_main/$flow_content/dateFrom
${selEndDate}     /$flow_main/$flow_content/dateTo
# ~~~~~~~ buttons
${btnAbort}       /$flow_main/$flow_content/abort
${btnConfirm}     /$flow_main/$flow_content/confirm
${btnGenerate}    /$flow_main/$flow_content/generate
# ~~~~~~~ mandator list
${listMandator}    /$flow_main/$flow_content/mandator_list
# ~~~~~~~ mandator col
${colIntern}      ${listMandator}/$col_intName
${colExtern}      ${listMandator}/$col_extName

*** Test Cases ***
Check GUI Elements Auswertung Screen1
    [Documentation]    MM 01.04.2018, BILL -> Auswertung: Check GUI Elements for Auswertung
    [Tags]    GUI_Elements    TextLanguagedependent
    ${langunge_code}=    Get selected LanguageCode
    #title
    Run Keyword And Continue On Failure    Check ContainsText    &{Dic_Title}[${langunge_code}]
    Run Keyword And Continue On Failure    Check ContainsText    &{Dic_Info}[${langunge_code}]
    #grid
    Run Keyword And Continue On Failure    Check ElementId    ${colIntern}    &{Dic_colMandantorIntern}[${langunge_code}]    enabled=true
    Run Keyword And Continue On Failure    Check ElementId    ${colExtern}    &{Dic_colMandantorExtern}[${langunge_code}]    enabled=true
    #buttons
    Run Keyword And Continue On Failure    Check ElementId    ${btnConfirm}    &{Dic_btnConfirm}[${langunge_code}]    enabled=false
    Run Keyword And Continue On Failure    Check ElementId    ${btnAbort}    &{Dic_btnAbort}[${langunge_code}]    enabled=true

Check Text Languagedependent Screen1
    [Documentation]    MM 01.04.2018, BILL -> Auswertung: Check texte in allen verfügbaren sprachen
    [Tags]    TextLanguagedependent    ChangeLanguage
    : FOR    ${key}    IN    @{AVAILABLE_LANGUAGES.keys()}
    \    Set LanguageId    ${key}
    \    Check LanguageId is selected    ${key}
    \    ${langunge_code}=    Get selected LanguageCode
    \    Run Keyword And Continue On Failure    Check ContainsText    &{Dic_Title}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Check ContainsText    &{Dic_Info}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Element Text Should Be    ${colIntern}    &{Dic_colMandantorIntern}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Element Text Should Be    ${colExtern}    &{Dic_colMandantorExtern}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Element Text Should Be    ${btnConfirm}    &{Dic_btnConfirm}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Element Text Should Be    ${btnAbort}    &{Dic_btnAbort}[${langunge_code}]
    Set LanguageId    ${DEFAULT_LANGUAGE_ID}

Check Sort By Mandant-intern
    [Documentation]    BILL -> Auswertung -> listMandator: Sortierung nach Spalte "Mandant-intern."
    [Tags]    Sorting
    Check Sorting Table By Columnname    ${listMandator}    $intName

Check Sort By Mandant-extern
    [Documentation]    BILL -> Auswertung -> Übersicht: Sortierung nach Spalte "Mandant-extern."
    [Tags]    Sorting
    Check Sorting Table By Columnname    ${listMandator}    $extName

Check No Mandator Selected Screen1
    [Documentation]    MM 01.04.2018, BILL -> Auswertung: Status der Buttons, wenn kein Element selektiert ist
    [Tags]    Action
    ${isSelected}    Check List has selection    ${listMandator}
    Should Be Equal    ${isSelected}    ${FALSE}
    Check ElementId    ${btnConfirm}    enabled=false
    Check ElementId    ${btnAbort}    enabled=true

Click Button Auswahl bestätigen Screen1
    [Documentation]    MM 01.04.2018, BILL -> Auswertung: Status der Buttons, wenn ein Element selektiert ist
    [Tags]    Action
    Select Mandator in Auswertung
    Click Element And Wait    ${btnConfirm}
    Check PageArea is visible    ${menu_content}    screen look_rlx_screen_bill_generally_evaluation_fromto

Click Button Abbruch Screen1
    [Documentation]    MM 01.04.2018, BILL -> Auswertung abbrechen
    [Tags]    Action
    Click Element And Wait    ${btnAbort}
    Check PageArea is visible    ${menu_content}    ${evaluation_class}    visible=false
    Check Homescreen is selected

Check GUI Elements Auswertung Screen2
    [Documentation]    MM 01.04.2018, BILL -> Auswertung: Check GUI Elements for Auswertung
    [Tags]    GUI_Elements    TextLanguagedependent
    ${langunge_code}=    Get selected LanguageCode
    Go To Screen2
    #title
    Run Keyword And Continue On Failure    Check ContainsText    &{Dic_Title}[${langunge_code}]
    Run Keyword And Continue On Failure    Check ContainsText    &{Dic_InfoSelectDate}[${langunge_code}]
    #date
    Run Keyword And Continue On Failure    Check DateSelector    ${selStartDate}    labeltext=&{Dic_startDate}[${langunge_code}]    enabled=false
    Run Keyword And Continue On Failure    Check DateSelector    ${selEndDate}    labeltext=&{Dic_endDate}[${langunge_code}]    enabled=false
    #buttons
    Run Keyword And Continue On Failure    Check ElementId    ${btnGenerate}    &{Dic_btnGenerate}[${langunge_code}]    enabled=false
    Run Keyword And Continue On Failure    Check ElementId    ${btnAbort}    &{Dic_btnAbort}[${langunge_code}]    enabled=true

Check Text Languagedependent Screen2
    [Documentation]    MM 01.04.2018, BILL -> Auswertung: Check texte in allen verfügbaren sprachen
    [Tags]    TextLanguagedependent    ChangeLanguage
    Go To Screen2
    : FOR    ${key}    IN    @{AVAILABLE_LANGUAGES.keys()}
    \    Set LanguageId    ${key}
    \    Check LanguageId is selected    ${key}
    \    ${langunge_code}=    Get selected LanguageCode
    \    Run Keyword And Continue On Failure    Check ContainsText    &{Dic_Title}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Check ContainsText    &{Dic_InfoSelectDate}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Check ContainsText    &{Dic_startDate}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Check ContainsText    &{Dic_endDate}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Element Text Should Be    ${btnGenerate}    &{Dic_btnGenerate}[${langunge_code}]
    \    Run Keyword And Continue On Failure    Element Text Should Be    ${btnAbort}    &{Dic_btnAbort}[${langunge_code}]
    Set LanguageId    ${DEFAULT_LANGUAGE_ID}

Check Startdatum and Enddatum is not selected Screen2
    [Documentation]    MM 01.04.2018, BILL -> Auswertung: kein datum ausgewählt
    [Tags]    Action    DatumSelector
    Go To Screen2
    Check Date Is Selected    ${selStartDate}    selected=false
    Check Date Is Selected    ${selEndDate}    selected=false
    Check ElementId    ${btnGenerate}    enabled=false
    Check ElementId    ${btnAbort}    enabled=true

Check Startdatum and Enddatum is not editable Screen2
    [Documentation]    MM 01.04.2018, BILL -> Auswertung: datum ist nicht editierbar
    [Tags]    Action    DatumSelector
    Go To Screen2
    ${passed} =    Run Keyword And Return Status    Edit DateValue In DateSelector    ${selStartDate}    01.04.2018
    Should Be Equal    ${passed}    ${FALSE}
    ${passed} =    Run Keyword And Return Status    Edit DateValue In DateSelector    ${selEndDate}    07.04.2018
    Should Be Equal    ${passed}    ${FALSE}

Check Startdatum and Enddatum is selectable Screen2
    [Documentation]    MM 01.04.2018, BILL -> Auswertung: datum ist nicht editierbar
    [Tags]    Action    DatumSelector
    Go To Screen2
    Check Date Is Selectable    ${selStartDate}
    Check Date Is Selectable    ${selEndDate}

Click Button Bericht erstellen Screen2
    [Documentation]    MM 01.04.2018, BILL -> Auswertung: Bericht erstellen
    [Tags]    Action    DatumSelector
    Go To Screen2
    Set Date In DateSelector    ${selStartDate}    13.01.2018
    Check Expected DateValue In DateSelector    ${selStartDate}    13.01.2018
    Check Date Is Selected    ${selStartDate}
    Check ElementId    ${btnGenerate}    enabled=false
    Set Date In DateSelector    ${selEndDate}    17.09.2018
    Check Expected DateValue In DateSelector    ${selEndDate}    17.09.2018
    Check Date Is Selected    ${selEndDate}
    Check ElementId    ${btnGenerate}    enabled=true
    Click Element And Wait    ${btnGenerate}
    Check PageArea is visible    ${menu_content}    ${evaluation_class}    visible=false
    Check Homescreen is selected

Click Button Abbruch Screen2
    [Documentation]    MM 01.04.2018, BILL -> Auswertung abbrechen
    [Tags]    Action    Fehlerhaft
    Go To Screen2
    Click Element And Wait    ${btnAbort}
    Check PageArea is visible    ${menu_content}    ${evaluation_class}    visible=false
    Check Homescreen is selected

*** Keywords ***
Setup Auswertung
    Select Auswertung    true
    Create Dictionaries For Auswertungen

Select Mandator in Auswertung
    ${ElementId}    Get ElementID from List    ${listMandator}    $intName    ${DEFAULT_MANDATOR_NAME}
    Click Element And Wait    ${ElementId}
    Check List ElementId is selected    ${ElementId}    sel

Go To Screen2
    Select Mandator in Auswertung
    Click Element And Wait    ${btnConfirm}

Teardown Auswertung
    ${passed} =    Run Keyword And Return Status    Select Auswertung
    Return From Keyword If    ${passed}
    Log    Die Seite war nicht clickable daher muss der Browser geschlossen und neugestartet werden    WARN
    Close Browser
    Select Auswertung    true

Create Dictionaries For Auswertungen
    Create SettingsDics    TotalAcount    Headline    Dic_Title
    Create SettingsDics    TotalAcount    SelectMandator    Dic_Info
    Create SettingsDics    TotalAcount    MandatorNameIntern    Dic_colMandantorIntern
    Create SettingsDics    TotalAcount    MandatorName    Dic_colMandantorExtern
    Create SettingsDics    TotalAcount    BTN_ConfirmSelection    Dic_btnConfirm
    Create SettingsDics    TotalAcount    BTN_AbortSelection    Dic_btnAbort
    Create SettingsDics    TotalAcount    SelcetStartDateAndEndDate    Dic_InfoSelectDate
    Create SettingsDics    TotalAcount    StartDate    Dic_startDate
    Create SettingsDics    TotalAcount    EndDate    Dic_endDate
    Create SettingsDics    TotalAcount    BTNCreateReportTotalAcounting    Dic_btnGenerate
